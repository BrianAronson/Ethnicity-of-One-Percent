PROC DATASETS lib=work kill nolist memtype=data;
quit;

*Define SCF Library;
libname Scf 'C:\Users\bdaro_000\Sociology\IndependentStudyRA\SIPP and SCF Merge Project\SCF Data Files';	
libname SIPP 'C:\Users\bdaro_000\Sociology\IndependentStudyRA\SIPP and SCF Merge Project\SIPP Data Files';	

*Combine data and create year variables- 
In SCF: indsname creates temporary variable of the name of each dataset, the following line creates a year variable based off the temporary name variable and the first occurence ofa number
In SIPP: Also creates a variable to identify the reference person;

*%Include 'C:\Users\bdaro_000\Sociology\IndependentStudyRA\SIPP and SCF Merge Project\Prep Data.sas';

*Sort SIPP by year and race for Proc "by" statements;
*Proc Sort Data=Sipp.Combined2;
*	by year erace;
*run;

*SIPP: Generate new temporary variables for analyses, convert missing to ".";
Data SippTemp;
	set Sipp.combined2;
	*Missing to ".";
	Array variables _numeric_;
		do over variables;
		if variables=-1 then variables=.;
		end;
	*Education;
		if eeducate<39 then EducBelowHS=1;
		else if eeducate~=. then EducBelowHS=0;
	*Race;
		If eorigin=1 & (year=2008 | year=2004) then Latino=1;
		Else if eorigin=0 & (year=2008 | year=2004) then Latino=0;
		Else if eorigin>19 & eorigin<30 then Latino=1;
		Else Latino=0;
		if erace=1 & Latino~=1 then White=1;
		else if erace~=. then White=0;
		if erace=2 & Latino~=1 then Black=1;
		else if erace~=. then Black=0;
		if erace=3 & Latino~=1 then NativeAmerican=1;
		else if erace~=. then NativeAmerican=0;
		if erace=4 & Latino~=1 then Asian=1;
		else if erace~=. then Asian=0;

	*Age;
		if tage>24 & tage<65 then Age25to64=1;
		Else if tage~=. then Age25to64=0;
	*Married;
		if ems=1 then Married=1;
		else if ems~=. then Married=0;
	*Children;
		if rfnkids<5 then Children=rfnkids;
		else if rfnkids~=. then Children=5;
	*Wealth Variables;
		Networth=thhtnw;
		Debt=thhdebt;
		Assets=Networth+Debt;
	*Home and Mortgage;
		HomeEquity=thhtheq;
		HomeDebt=thhmortg;
		if thhtheq~=0 then HomeOwner=1;
		else if thhtheq=. | thhtheq=0 then Homeowner=0;
		if HomeOwner=1 & enummort=. then Mortgages=0;
		else if Homeowner=1 then Mortgages=enummort;
	*Liquid Asset Rates of Ownership; 
		if east3a=1 | east3b=1 then StockMutual=1;
		else if east3a=2 & east3b=2 then StockMutual=0;
		if east2a=1 then InterestBank=1;
		else if east2a=2 then InterestBank=0;
		if ealich=1 then Checking=1;
		else if ealich=2 then Checking=0;
		If east1b=1 | east1c=1 then IRAor401k=1;
		Else if east1b=2 | east1c=2 then IRAor401k=0;
	*Inflate variables by year;
		Array Inflation (6) Income Networth Debt Assets HomeEquity HomeDebt;
		Do i = 1 to 6;
			IF year=1996 then Inflation(i)=Inflation(i)*233.916/154.4;
			IF year=2001 then Inflation(i)=Inflation(i)*233.916/175.1;
			IF year=2004 then Inflation(i)=Inflation(i)*233.916/185.2;
			IF year=2008 then Inflation(i)=Inflation(i)*233.916/214.429;
		END;
run;
*Sipp Demographics;
Proc Means mean Data=SippTemp;	
	var EducBelowHS Age25to64 Married Children White Black NativeAmerican Asian Latino;
	by year;
	Weight wpfinwgt;
	where refperson=1;
run;
*Sipp Income and Wealth;
Proc Means mean median P90 P99 Data=SippTemp;	
	var Income Networth Debt Assets;
	by year;
	Weight whfnwgt;
	*where tage>34 & tage<55 & white=1;	
run;
*Sipp Homeownership;
Proc Means mean median P90 P99 Data=SippTemp;	
	var Homeowner Mortgages HomeEquity HomeDebt;
	by year;
	Weight whfnwgt;
	*where White=1;
	*where tage=24 & tage<65;
run;
*Liquid assets;
Proc Means mean median P90 P99 Data=SippTemp;	
	var StockMutual InterestBank Checking IRAor401k;
	by year;
	Weight whfnwgt;
	*where White=1;
	*where tage=24 & tage<65;
run;

Data SCFTemp;
	Set SCF.Combined;
	*Education;
		if educ<12 then EducBelowHS=1;
		else EducBelowHS=0;
	*Race;
		if race=1 then White=1;
		else White=0;
		if race=2 then Black=1;
		else Black=0;
		if race=3 then Hispanic=1;
		else Hispanic=0;
		if race=5 then Other=1;
		else Other=0;
	*Age;
		if age>24 & age<65 then Age25to64=1;
		Else Age25to64=0;
	*Married;
		if Married=2 then Married=0;
	*Children;
		if kids<5 then Children=kids;
		else Children=5;
	*Wealth Variables;
		Assets=Networth+Debt;
	*Home and Mortgage;
		HomeEquity=homeeq;
		HomeDebt=NH_MORT;
		if homeeq~=0 then HomeOwner=1;
		else if homeeq=. | homeeq=0 then Homeowner=0;
		if housecl~=2 then HomeOwner1=1;
		else Homeowner1=0;
		if HomeOwner=1 & PAYMORT1=0 then Mortgages=0;
		else if Homeowner=1 & PAYMORT1>0 & PAYMORT2>0 & PAYMORT3>0 then Mortgages=3 ;
		else if Homeowner=1 & PAYMORT1>0 & PAYMORT2>0 & PAYMORT3=0 then Mortgages=2 ;
		else if Homeowner=1 & PAYMORT1>0 & PAYMORT2=0 & PAYMORT3=0 then Mortgages=1 ;
	*Liquid Asset Rates of Ownership; 
		if hstocks=1 | deq>0 then StockMutual=1;
		else StockMutual=0;
		if mma>0 then InterestBank=1;
		else InterestBank=0;
		if nochk=0 then Checking=1;
		else Checking=0;
		If irakh>0 then IRAor401k=1;
		Else  IRAor401k=0;
run;
*SCF Demographics;
Proc Means mean Data=SCFTemp;	
	var EducBelowHS Age25to64 Married Children White Black Hispanic Other ;
	by year;
	Weight wgt;
run;
*SCF Income and Wealth;
Proc Means mean median P90 P99 Data=SCFTemp;	
	var Income Networth Debt Assets;
	by year;
	Weight wgt;
	*where age>34 & age<55 & white=1;	
run;
*SCF Homeownership;
Proc Means mean median P90 P99 Data=SCFTemp;	
	var Homeowner Mortgages HomeEquity HomeDebt;
	by year;
	Weight wgt;
run;
*SCF Liquid assets;
Proc Means mean median P90 P99 Data=SCFTemp;	
	var StockMutual InterestBank Checking IRAor401k;
	by year;
	Weight wgt;
run;

*Other Diagnostics;

*Comparing ages;
proc means data=SIPPTEMP;
	var tage;
	where refperson=1 & tage>34 & tage<55 & white=1;	
run;
proc means data=SCFTEMP;
	var age;
	where age>34 & age<55 & white=1;
run;
*Generate SCF income and networth top percentiles;
proc univariate data=SCFTEMP;
   var income networth;
   output out=percentiles1 pctlpts=99 98 95 90 pctlpre=income networth;
   by year;
   weight wgt;
run;
*Change years to match SIPP;
DATA Percentiles1;
	set Percentiles1;
	if year=1995 then year=1996;
	if year=2007 then year=2008;
run;
*Merge Percentiles with SIPP, create SIPP income categories to count;
DATA SIPPTEMP;
	merge SIPPTEMP Percentiles1;
	by year;
	if Income>=income99 then SIPPinc99=1;
	if Income>=income98 then SIPPinc98=1;
	if Income>=income95 then SIPPinc95=1;
	if Income>=income90 then SIPPinc90=1;
	if networth>=networth99 then SIPPnet99=1;
	if networth>=networth98 then SIPPnet98=1;
	if networth>=networth95 then SIPPnet95=1;
	if networth>=networth90 then SIPPnet90=1;
run;
*Find frequencies of income categories;
proc freq data=sipptemp;
	tables SIPPnet90 SIPPnet95 SIPPnet98 SIPPnet99 SIPPinc90 SIPPinc95 SIPPinc98 SIPPinc99;
	where refperson=1;
run;
